plugins {
  id "idea"
  id "eclipse"
  //// gradle dependencyUpdates -Drevision=release
  id "com.github.ben-manes.versions" version "0.14.0"
  //id "com.avast.gradle.docker-compose" version "0.3.21"
  id "io.spring.dependency-management" version "1.0.2.RELEASE" apply false
}

allprojects {

  apply plugin: "java"
  apply plugin: "maven"

  group twitterGroup
  version twitterVersion

  jar {
    group = twitterGroup
    version = twitterVersion
  }

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  repositories {
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }
}

subprojects {

  apply plugin: "io.spring.dependency-management"

  dependencies {
    compile "org.springframework.cloud:spring-cloud-starter-stream-rabbit",
        "org.springframework.cloud:spring-cloud-starter-bus-amqp",
        "org.springframework.cloud:spring-cloud-starter-config",
        "org.springframework.cloud:spring-cloud-starter-eureka",
        "org.springframework.cloud:spring-cloud-starter-hystrix",
        "org.springframework.boot:spring-boot-starter-actuator",
        "org.projectlombok:lombok"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    runtime "org.springframework.boot:spring-boot-devtools"
  }

  test {
    dependsOn ":cloud-infrastructure:compose:composeUp"
    finalizedBy ":cloud-infrastructure:compose:composeDown"
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:1.5.3.RELEASE"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.BUILD-SNAPSHOT"
      mavenBom "org.springframework.cloud:spring-cloud-stream-dependencies:Chelsea.BUILD-SNAPSHOT"
    }
  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ["alpha", "beta", "rc", "cr", "m"].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject("Release candidate")
      }
    }
  }
}
